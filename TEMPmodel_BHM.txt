
  model {
    for ( i in 1:Ntotal ) {
    # top level: y is a Bernoulli random varable
      y[i] ~ dbern( theta[s[i]] )
    }
    for ( sIdx in 1:Nsubj ) {
    # for each group, designate a different theta with beta distribution
      theta[sIdx] ~ dbeta( omega*(kappa-2)+1 , (1-omega)*(kappa-2)+1 ) 
    }
    # parameter for the beta distribution
    omega ~ dbeta( 1 , 1 ) # broad uniform
    # omega ~ dbeta( 5001 , 15001 ) # Skeptical prior for ESP
    # more hierarchy for kappa
    kappa <- kappaMinusTwo + 2
    # kappaMinusTwo ~ dgamma( 0.01 , 0.01 )  # mean=1 , sd=10 (generic vague)
    kappaMinusTwo ~ dgamma( 1.105125 , 0.1051249 )  # mode=1 , sd=10 
    # kappaMinusTwo ~ dgamma( 36 , 0.12 )  # mode=300 , sd=50 : skeptical 
  }
